#Structure overview of the core classes

= masapi core structure =

Here is the basic structure of the core classes of masapi :

http://masapi.googlecode.com/svn/wiki/pictures/uml_masapi.jpg

  * blue : interfaces
  * blank : classes
  * gray : internal classes

Both `IMassLoader` and `ILoadableFile` extends the same interface `ILoadManager` (which extends `IEventDispatcher`) in order to provide the same type of events.

== The ILoadableFile interface ==

The target of this interface is to group the objects that provides some generic events during a load operation such as `Event.OPEN`, `ProgressEvent.PROGRESS`, `Event.COMPLETE`, ... In order to do that, the classes that implements it just encapsulates some load manager objects (`URLLoader`, `Loader`, `Sound`, `URLStream`) and redirect their events to match the interface declaration.

This interface is implemented by some internal classes into the `ch.capi.net` package and are not directly accessible trough the implementation. However, the `ILoadableFile` interface allows you to access the load manager object (see the `loadManagerObject` property). Currently, all the implementations of `ILoadableFile` extends the `AbstractLoadableFile` class because it provides the basic data needed by the interface. It is the `LoadableFileFactory` who will knows what type of implementation of the `ILoadableFile` interface you need (it depends on the load manager object you want to use).

== Creating a ILoadableFile implementation ==

The easiest way to do it is to extends the `AbstractLoadableFile` class and implements the `ILoadableFile` interface. After that, there are only some methods such as `start()` or `stop()` to implement. Here is for example the implementation of the `SLoadableFile` that encapsulates a `Sound` object :
{{{
package ch.capi.net
{
	import flash.events.IEventDispatcher;
	import flash.media.Sound;
	import flash.media.SoundLoaderContext;
	import flash.net.URLRequest;
	
	import ch.capi.net.LoadableFileType;
	
	/**
	 * Represents a <code>ILoadableFile</code> based on a <code>Sound</code> object.
	 * 
	 * @author	Cedric Tabin - thecaptain
	 * @version	1.0
	 */
	internal class SLoadableFile extends AbstractLoadableFile implements ILoadableFile
	{
		//---------//
		//Variables//
		//---------//
		
		/**
		 * Defines the <code>SoundLoaderContext</code> to be used
		 * within the loaded <code>Sound</code>.
		 */
		public var soundLoaderContext:SoundLoaderContext = new SoundLoaderContext();
		
		//-----------//
		//Constructor//
		//-----------//
		
		/**
		 * Creates a new <code>SLoadableFile</code> object.
		 * 
		 * @param	snd		The <code>Sound</code> object.
		 */
		public function SLoadableFile(snd:Sound):void
		{
			super(snd);
			
			registerTo(snd);	
		}
		
		//--------------//
		//Public methods//
		//--------------//
		
		/**
		 * Starts the loading of the data.
		 */
		public override function start():void
		{
			super.start();
			
			var re:URLRequest = getURLRequest();
			var sd:Sound = loadManagerObject as Sound;
			sd.load(re, soundLoaderContext);
		}
		
		/**
		 * Stops the loading of the data.
		 */
		public override function stop():void
		{
			super.stop();
			
			var sd:Sound = loadManagerObject as Sound;
			sd.close();
			
			super.close();
		}
		
		/**
		 * Retrieves the <code>IEventDispatcher</code> of all the sub-events
		 * of a <code>ILoadableFile</code>. For example, the source event dispatcher
		 * of a <code>Loader</code> object will be his <code>contentLoaderInfo</code>.
		 * 
		 * @return	The <code>URLLoader</code> object.
		 */
		public function getEventDispatcher():IEventDispatcher
		{
			return loadManagerObject as Sound;
		}
		
		/**
		 * Retreives the data of the <code>loadManagerObject</code> if the loading
		 * is complete.
		 * 
		 * @return	The data of the <code>loadManagerObject</code>.
		 */
		public function getData():*
		{
			return loadManagerObject as Sound;
		}
		
		/**
		 * Retrieves the type of the file based on the <code>LoadableFileType</code> constants.
		 * 
		 * @return	The <code>ILoadableFile</code> type.
		 */
		public function getType():String
		{
			return LoadableFileType.SOUND;
		}
	}
}
}}}